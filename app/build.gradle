plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "name.remal.check-updates"
    id "kotlinx-serialization"
}
apply from: 'https://raw.githubusercontent.com/JakeWharton/SdkSearch/master/gradle/projectDependencyGraph.gradle'

android {
    defaultConfig {
        applicationId "com.example.pokemonmuiltimodulearch"
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

def allTestsConfigs(dependency) {
    dependencies.add('testImplementation', dependency)
    dependencies.add('androidTestImplementation', dependency)
}

def allKaptTestsConfigs(dependency) {
    dependencies.add('kaptAndroidTest', dependency)
    dependencies.add('kaptTest', dependency)
}


dependencies {
    implementation project(':libraries:shared-ui')
    implementation project(':libraries:core')
    implementation project(':features:feature-a:feature-a-public')
    implementation project(':features:feature-a:feature-a-private')
    implementation project(':features:auth:auth-public')
    implementation project(':features:auth:auth-private')

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Hilt
    implementation HiltDeps.hilt_impl_dep
    kapt HiltDeps.hilt_kapt_compiler
    allTestsConfigs(HiltDeps.hilt_test)
    allKaptTestsConfigs(HiltDeps.hilt_kapt_test)

    // FragNav
    implementation Deps.frag_nav

    // Retrofit
    implementation Deps.retrofit
    implementation Deps.ok_http
    implementation Deps.ok_http_interceptor
    implementation Deps.gson_converter
    implementation Deps.serialization_dep
    implementation Deps.serializationx_converter
    implementation Deps.androidx_core
}

kapt {
    correctErrorTypes true
}