plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "name.remal.check-updates"
}

android {
    defaultConfig {
        buildConfigField "boolean", "MOCK_WEBSERVER", "true"
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.1'
    }
}

def allTestsConfigs(dependency) {
    dependencies.add('testImplementation', dependency)
    dependencies.add('androidTestImplementation', dependency)
}

def allKaptTestsConfigs(dependency) {
    dependencies.add('kaptAndroidTest', dependency)
    dependencies.add('kaptTest', dependency)
}

dependencies {
    implementation project(':libraries:shared-ui')
    implementation project(':features:feature-a:feature-a-public')
    implementation project(':libraries:core')
    implementation project(':libraries:shared-utils')
    implementation project(':features:auth:auth-public')
    implementation project(':libraries:test-utils')

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Compose
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.0.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.0.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.0.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.0.1'
    // When using a MDC theme
    implementation "com.google.android.material:compose-theme-adapter:1.0.1"

    implementation Deps.androidx_core

    // KTX
    implementation Deps.collection_ktx
    implementation Deps.fragment_ktx
    implementation Deps.lifecycle_ktx
    implementation Deps.liveData_ktx
    implementation Deps.viewModel_ktx

    // Hilt
    implementation HiltDeps.hilt_impl_dep
    implementation HiltDeps.hilt_viewmodel
    kapt HiltDeps.hilt_kapt_compiler
    allTestsConfigs(HiltDeps.hilt_test)
    allKaptTestsConfigs(HiltDeps.hilt_kapt_test)

    // Coroutines
    implementation CoroutinesDeps.coroutine_core
    implementation CoroutinesDeps.coroutine_android
    implementation CoroutinesDeps.legacy_support

    // Retrofit
    implementation Deps.retrofit

    allTestsConfigs("androidx.arch.core:core-testing:2.1.0")
    allTestsConfigs("com.google.truth:truth:1.0.1")
    testImplementation "io.mockk:mockk:1.12.0"


}